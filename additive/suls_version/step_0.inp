* construct and heat box of arbitrary liquid
* streams files named by input parameters "vars" and "consts"
*

!First variables, then constants!
set toppar toppar
stream toppar.str 
system "mkdir -p step0-initbox"

! Simulation Parameters

set eqbox 3 ! read box after 3 * 400ps eq
set nstep 100000 ! 50 ps eq + 50 ps prod
set rseed 2000000011 1400000023 900050009 444444443
set avers 10000 ! 5 Production Snapshots of Monomers
set nprint 10000 ! 5 Production Snapshots of Monoemrs
set tempr 301
set press 1
set first none
set last none
set nmolx 6

! Residue Variables
set residue hexa
set residue_mp2 hexa.crd

!### Simulation params ###
set timestep 0.001
set 3 14.0  ! cutim
set 4 14.0  ! cutnb
set 5 10.0  ! ctonnb
set 6 12.0  ! ctofnb
set 7 switch
set 8 atom
set 9 vatom

!Calculate crystal lattice
calc xlatx ( 86.18 * 1.660577881 / 0.659 )**0.3333333333
calc nmol @nmolx**3
calc xlat @xlatx * @nmolx
calc ctnbmax @xlat / 2.0 - 1.0
!!Olgun says this test has become redundant and it's causing a lot of pain
!if @4 .gt. @ctnbmax stop

!Generate monomer
read sequence @residue 1
generate @residue first @first last @last setup warn
open unit 20 read form name @residue_mp2.crd ! minimized additive monomer geometry
read coor card unit 20

!ic param
!ic seed @zeed
!ic build
!coor init selec ( hydrogen ) end
!hbuild
coor shake

!Minimize monomer
coor orie norot
mini sd nstep 1000

!Write monomer
open unit 1 write form name @residue.monomer.psf
write psf card unit 1
* @residue monomer
*

close unit 1
open unit 1 write form name @residue.monomer.crd
write coor card unit 1
* @residue monomer
*

close unit 1
open unit 1 write form name @residue.monomer.pdb
write coor pdb unit 1
* @residue monomer
*
close unit 1
delete atom select all end

!Generate box
read sequence @residue @nmol
generate @residue first @first last @last setup warn
open unit 1 write form name @residue.box.psf
write psf card unit 1
* box of @nmol @residue
*
close unit 1

set i 1
set ii 1
label coori
  calc delx  @i * @xlatx
  set j 1
  label coorj
    calc dely  @j * @xlatx
    set k 1
    label coork
  
      open unit 1 read form name @residue.monomer.crd
      read coor card append unit 1 ! select ( ires @i ) end
      calc delz  @k * @xlatx
      coor trans xdir @delx ydir @dely zdir @delz select ( ires @ii ) end

      incr ii by 1
      incr k by 1
      if @k .le. @nmolx goto coork

    incr j by 1
    if @j .le. @nmolx goto coorj

  incr i by 1
  if @i .le. @nmolx goto coori

!Write initial box
coor orie norot
open unit 1 write form name @residue.box.pdb
write coor pdb unit 1
* box of @nmol @residue
* 

!Set up PBC
crystal define cubic @xlat @xlat @xlat 90.00 90.00 90.00
crystal build Noper 0 cutoff @3
image byres xcen 0.0 ycen 0.0 zcen 0.0 sele resn @residue end

!note inclusion of long-range correction
update inbfrq -1 imgfrq -1 ihbfrq 0 -
ewald pmewald kappa 0.34 fftx 32 ffty 32 fftz 32 order 4 -
lrc @7 @8 @9 vfswitch cutimg @3 cutnb @4 ctofnb @6 ctonnb @5 bycb

!Minimize box
mini sd nstep 10000 nprint 1000

shake bonh para

!Write minimized box
open unit 1 write form name @residue.box.min.pdb
write coor pdb unit 1
* minimized box of @nmol @residue
* 
open unit 1 write form name @residue.box.min.crd
write coor card unit 1
* minimized box of @nmol @residue
* 

!Heat up box
!open unit 31 write form name @scratch/@residue.box.0.res
open unit 31 write form name step0-initbox/@residue.box.0.res
open unit 32 write unfo name step0-initbox/@residue.box.0.dcd
dynamics cpt leap start time @timestep nstep 100000 iseed @rseed -
  firstt @tempr finalt @tempr tstruc @tempr -
  inbfrq -1 imgfrq -1 ntrfrq 1000 iprfrq @avers nprint @nprint -
  iasors 1 iasvel 1 ieqfrq 100 twindl -1.0 twindh 1.0 -
  iunrea 30 iunwri 31 iuncrd 32 kunit -1 nsavc 10000 isvfrq 10000 -
  pconstant pint pref @press pmass 1000.0 pgamma 25.0 tbath @tempr
  
!Write heated box
open unit 20 write form name step0-initbox/@residue.box.0.crd
write coor card unit 20
* @residue after heating to @tempr K
* lattice size: ?xtla
*

open unit 30 read form name step0-initbox/@residue.box.0.res
open unit 31 write form name step0-initbox/@residue.box.1.res
open unit 32 write unfo name step0-initbox/@residue.box.1.dcd
dynamics cpt leap restart time @timestep nstep @nstep iseed @rseed -
  firstt @tempr finalt @tempr tstruc @tempr -
  inbfrq -1 imgfrq -1 ntrfrq 1000 iprfrq @avers nprint @nprint -
  iasors 1 iasvel 1 ieqfrq 100 twindl -1.0 twindh 1.0 -
  iunrea 30 iunwri 31 iuncrd 32 kunit -1 nsavc 10000 isvfrq 10000 -
  pconstant pint pref @press pmass 1000.0 pgamma 25.0 tbath @tempr
  
!Write heated box
open unit 20 write form name step0-initbox/@residue.box.1.crd
write coor card unit 20
* @residue after heating to @tempr K
* lattice size: ?xtla
*

!Write heated box
open unit 20 card form name step0-initbox/@residue.box.1.pdb
write coor pdb unit 20
* @residue after heating to @tempr K
* lattice size: ?xtla
*
